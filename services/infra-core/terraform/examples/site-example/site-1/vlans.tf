# VLANs para o Site Nordeste
# Este arquivo cria VLANs usando um mapa centralizado

# Local variables para VLANs
locals {
  # Mapeamento de VLANs por categoria
  vlans = {
    # Management Fabric (100-199)
    "oob_bmc" = {
      vid         = 100
      name        = "VLAN 100 - OOB/BMC"
      description = "VLAN OOB (BMC network) para o ${local.site_name}"
      group_id    = module.vlan_group_100.id
    }
    "pxe" = {
      vid         = 101
      name        = "VLAN 101 - PXE"
      description = "VLAN PXE para o ${local.site_name}"
      group_id    = module.vlan_group_100.id
    }
    "oam" = {
      vid         = 102
      name        = "VLAN 102 - OAM"
      description = "VLAN OAM (operations, access, monitoring; Inbound) [default route] para o ${local.site_name}"
      group_id    = module.vlan_group_100.id
    }
    "bgp" = {
      vid         = 103
      name        = "VLAN 103 - BGP"
      description = "VLAN BGP para o ${local.site_name} - BGP needs to be enabled on these VLANs"
      group_id    = module.vlan_group_100.id
    }
    
    # Data Networks (250-299)
    "overlay" = {
      vid         = 250
      name        = "VLAN 250 - Overlay network (tunneling)"
      description = "VLAN Overlay network (tunneling) para o ${local.site_name}"
      group_id    = module.vlan_group_200.id
    }
    "api_internal" = {
      vid         = 251
      name        = "VLAN 251 - API internal network"
      description = "VLAN API internal network - connection between IaaS API services and all other Incuses within the AZ (or even the region) para o ${local.site_name}"
      group_id    = module.vlan_group_200.id
    }
    "api_access" = {
      vid         = 252
      name        = "VLAN 252 - API access network"
      description = "VLAN API access network - routed network from users/firewall to Kong (contains data generated by users) para o ${local.site_name}"
      group_id    = module.vlan_group_200.id
    }
    "block_storage" = {
      vid         = 253
      name        = "VLAN 253 - Block Storage network"
      description = "VLAN Block Storage network - used for storage access by API, but also for ceph replication para o ${local.site_name}"
      group_id    = module.vlan_group_200.id
    }
    "object_storage_access" = {
      vid         = 254
      name        = "VLAN 254 - Object Storage Access network"
      description = "VLAN Object Storage Access network - between external users, Kong to be more precise, and Swift Proxies (contains data generated by users) para o ${local.site_name}"
      group_id    = module.vlan_group_200.id
    }
    "object_storage_replicate" = {
      vid         = 255
      name        = "VLAN 255 - Object Storage Replicate network"
      description = "VLAN Object Storage Replicate network - network for data replication within Swift para o ${local.site_name}"
      group_id    = module.vlan_group_200.id
    }
    "data_management" = {
      vid         = 256
      name        = "VLAN 256 - Data Management network"
      description = "VLAN Data Management network - network to manage stuff that is in telco rack (firewall VMs), and also serves as a backup network for OAM on management fabric para o ${local.site_name}"
      group_id    = module.vlan_group_200.id
    }
  }
}

# ====== Criação das VLANs usando for_each ======

module "vlans" {
  source = "../../../modules/netbox_vlan"
  
  for_each = local.vlans
  
  name        = each.value.name
  vid         = each.value.vid
  status      = local.status_vlan_prefix
  description = each.value.description
  tenant_id   = module.tenant_br-ne-1.id
  site_id     = module.site_br-ne-1.id
  group_id    = each.value.group_id
  tags        = [data.netbox_tag.tag_iac_network.name]
}

